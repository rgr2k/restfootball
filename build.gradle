plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'groovy'
	id "org.unbroken-dome.test-sets" version "2.2.1"
}

group = 'de.planerio'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//Spock
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.10', ext: 'pom'
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.2.5.RELEASE'
	testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5'
	testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
	testCompile group: 'com.jayway.restassured', name: 'rest-assured', version: '2.8.0'

	//Swagger
	compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

	compile 'com.h2database:h2:1.4.200'
	compile 'org.springframework.boot:spring-boot-starter-data-jpa:2.2.4.RELEASE'

	//Orika
	compile group: 'ma.glasnost.orika', name: 'orika-core', version: '1.5.4'
}

testSets {
	integrationTest { dirName = 'integTest' }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

integrationTest {
	useJUnit {
		includeCategories 'de.planerio.developertest.IntegTest'
	}
	testLogging {
		showStandardStreams = true
	}
}

test {
	useJUnit {
		includeCategories 'de.planerio.developertest.UnitTest'
	}
	testLogging {
		events "passed", "skipped", "failed"
		exceptionFormat "short"
		showCauses true
		showExceptions true

		// set options for log level DEBUG
		debug {
			events "started", "skipped", "failed"
			exceptionFormat "full"
		}
	}
}
